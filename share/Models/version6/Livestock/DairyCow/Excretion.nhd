*** general ***

author   = Agrammon Group
date     = 2008-03-30
taxonomy = Livestock::DairyCow::Excretion

+short

Computes the annual N excretion of a number of dairy cows as a function of the
milk yield and the feed ration.

+description

This process calculates the annual N excretion (total N and Nsol (urea plus
measured total ammoniacal nitrogen)) of a number of dairy cows as a
function of the milk yield and the supplied feed ration. Nitrogen
surpluses from increased nitrogen uptake are primarily excreted as
Nsol in the urine. Eighty percent of the increased N excretion is
therefore added to the Nsol fraction.

The standard N excretion was taken from the official Swiss fertilizer
guidelines. These values were compiled on the basis of official
feeding recommendations (RAP 1999) by a group of feeding experts under
the lead of H. Menzi. Even though the methodology used is not
documented in detail, it was well known to the authors of DYNAMO.

\subsubsection{References:}

Burgos SA, Robinson PH, Fadel JG, DePeters EJ 2005. Ammonia volatilization
potential: Prediction of urinary urea nitrogen output on lactating dairy
cows. Agriculture, Ecosystems and Environment 111:261-269.

Peterson SO, Sommer SG, Aaes O, Soegaard K 1998. Ammonia losses from urine and
dung of grazing cattle: effect of N intake. Atmospheric environment
32:295-300.

RAP 1999. Fütterungsempfehlungen und Nährwerttabelle für Wiederkäuer. 4. Überarbeitete Auflage, 327p, Landwirtschaftliche Lehrmittelzentrale, Zollikofen.

Flisch R, Sinaj S, Charles R, Richner W 2009. Grundlagen für die die Düngung im Acker- und Futterbau. Agrarforschung 16(2).

*** input ***

+animalcategory
  type = enum
  filter = true
  default_gui  = dairy_cows
  default_calc = dairy_cows
  ++enum
    +++dairy_cows
       en = Dairy_cows
       de = Milchkühe
       fr = Vaches laitières
  ++labels
    en = Animal category
    de = Tierkategorie
    fr = Catégorie d'animaux
  ++units
    en = -
++description
    Animal category
++help
  +++en
     <p>There are no sub categories for dairy cows.</p>
  +++de
     <p>Für Milchkühe sind keine Unterkategorien vorgesehen.</p>
  +++fr
     <p><Für Milchkühe sind keine Unterkategorien vorgesehen./p>

+animals
  type = float
  validator = ge(0)
  distribute = true
  ++labels
    en = Number of animals
    de = Anzahl Tiere
    fr = Nombre d'animaux
  ++units
    en = -
  ++description
    Number of dairy cows in barn.
  ++help
    +++en
       <p>Actual number of animals in the barn.</p>
    +++de
       <p>Tatsächliche Anzahl Tiere im Stall.</p>
    +++fr
       <p>Nombre effectif d'animaux dans la stabulation.</p>

+dimensioning_barn
  type = float
  distribute = true
  default_gui  = Standard
  default_formula = In(animals);
  ++labels 
    en = Number of available animal places
    de = Anzahl Tierplätze im Stall
    fr = Nombre de places dans l'étable
  ++units  
    en = -
  ++description
    Number of available animal places.
    ++help
      +++en
         <p>For loose housing systems: all animal places accessible for the animals must be entered. The number of animal places is higher than the stocking rate if reserve capacities are available, e.g. for a future increase of the herd size</p> 
         <p>If "Standard" is selected the number of available animal places is equal to the number of animals.</p>
      +++de
         <p>Für Laufställe: es sind sämtliche Tierplätze anzugeben, welche für die Tiere zugänglich sind. Die Anzahl Tierplätze im Stall ist grösser als der Bestand, wenn etwa im Hinblick auf eine zukünftige Vergrösserung des Betriebs zusätzliche Tierplätze vorhanden sind. Mehr Tierplätze als Tiere bedeuten für Laufställe eine Erhöhung der verschmutzten Fläche pro Tier. Im Gegensatz dazu führen mehr Tierplätze als Tiere bei Anbindeställen kaum zu einer Erhöhung der verschmutzten Fläche pro Tier, da die freien Tierplätze für die Tiere nicht zugänglich sind.</p>
         <p>Bei Auswahl "Standard" wird die Anzahl Tierplätze im Stall der eingegebenen Anzahl Tiere gleichgesetzt.</p>
      +++fr
         <p>Pour les stabulations libres: mentionner toutes les places accessibles aux animaux. Le nombre de places dans l'étable peut être supérieur au cheptel actuel, dans la perspective d'un agrandissement futur de l'exploitation p.ex. Dans les stabulations libres, un nombre de places supérieur au nombre d'animaux signifie une augmentation de la surface souillée par animal. Au contraire, dans les stabulations entravées, un nombre de places supérieur au nombre d'animaux ne conduit pas à une augmentation de la surface souillée par animal, vu que ces places libres ne sont pas accessibles aux animaux.  </p>
         <p>Si "Standard" est introduisé, la valeur du nombre d'animaux est utilisée.</p>

?if !Kantonal_LU
  +inp_n_excretion
    type = float
    default_calc = Standard
    default_gui  = Standard
    ++labels
      en = N excretion for dairy cows
      de = N Ausscheidung
      fr = Excrétions azotées
    ++units
      en = kg N/year
      de = kg N/Jahr
      fr = kg N/an
    ++description
      Annual standard N excretion for a dairy cow
    ++help
      +++en
         <p>If "Standard" is selected the standard value for the
            nitrogen excretion according to PRIF 2017 is applied (Richner
            et al., 2017) is applied (PRIF: Principles of Agricultural Crop
            Fertilisation in Switzerland). If a number is entered, a value
            for the nitrogen excretion can be selected by the user.</p>
         <p>Richner, W., Flisch, R., Mayer, J., Schlegel, P., Zähner,
            M., Menzi, H., 2017. 4/ Eigenschaften und Anwendung von
            Düngern, in: Richner, W., Sinaj, S. (Eds.), Grundlagen für
            die Düngung landwirtschaftlicher Kulturen in der Schweiz /
            GRUD 2017. Agrarforschung Schweiz 8 (6) Spezialpublikation,
            pp. 4/1-4/23.</p>
      +++de
         <p>Bei Eingabe <b>Standard</b> wird der Richtwert für die Stickstoffausscheidung gemäss GRUD
            2017 (Richner et al., 2017) verwendet. Bei Eingabe einer Zahl kann der Wert für die
            Stickstoffausscheidung selbst gewählt werden.</p>
         <p>Richner, W., Flisch, R., Mayer, J., Schlegel, P., Zähner,
            M., Menzi, H., 2017. 4/ Eigenschaften und Anwendung von
            Düngern, in: Richner, W., Sinaj, S. (Eds.), Grundlagen für
            die Düngung landwirtschaftlicher Kulturen in der Schweiz /
            GRUD 2017. Agrarforschung Schweiz 8 (6) Spezialpublikation,
            pp. 4/1-4/23.</p>
      +++fr
         <p>Si "Standard" est introduisé, la valeur de référence pour
            les excrétions azotées d'après PRIF 2017 (Richner et al., 2017)
            est utilisée (PRIF: « Principes de la fertilisation des
            cultures agricoles en Suisse » ). Si un nombre est introduisé
            une valeur pour les excrétions azotées peut être déterminée par
            l'utilisateur/-trice.</p>
          <p>Richner, W., Flisch, R., Mayer, J., Schlegel, P., Zähner,
            M., Menzi, H., 2017. 4/ Eigenschaften und Anwendung von
            Düngern, in: Richner, W., Sinaj, S. (Eds.), Grundlagen für
            die Düngung landwirtschaftlicher Kulturen in der Schweiz /
            GRUD 2017. Agrarforschung Schweiz 8 (6) Spezialpublikation,
            pp. 4/1-4/23.</p>

  +tan_fraction
    type = percent
    default_calc = Standard
    default_gui  = Standard
    ++labels
      en = TAN fraction of N excretion
      de = TAN Anteil der N Ausscheidung
      fr = Proportion de TAN des excrétions azotées
    ++units
      en = %
      de = %
      fr = %
    ++description
      TAN fraction of the annual standard N excretion
    ++help
      +++en
         <p>If "Standard" is selected ...</p>
      +++de
         <p>Bei Eingabe <b>Standard</b> wird ...</p>
      +++fr
         <p>Si "Standard" est introduisé, la valeur de ...</p>

?endif

*** technical ***

+standard_N_excretion
  value = 112
  ++units
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual standard N excretion for a dairy cow according to
    Flisch et al. (2009).

+share_Nsol
  value = 0.55
  ++units
    en  = -
  ++description
    Nsol content of excreta. Derived from e.g.
    Peterson et al. (1998) or Burgos et al. (2005).

+feed_influence_on_Nsol
  value = 1
  ++units
    en = kg Nsol/kg N
  ++description
    Proportion of N (calculated from feed ration correction) excreted as
    Nsol. Derived from e.g. Peterson et al. (1998).

*** external ***

+Excretion::CMilk
?if !Kantonal_LU
  +Excretion::CFeed
?endif

*** output ***

+animals
  print = DairyCow
  ++units
    en = -
  ++description
    Number of dairy cows in barn.
  ++formula
    In(animals);

+animalcategory
  print = DairyCow
  ++units
    en = -
  ++description
    Animal category
  ++formula
    In(animalcategory);

+n_excretion_animal
  print = DairyCow
  ++units
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual mean total N excreted per animal.
?if Kantonal_LU
  ++formula
    Tech(standard_N_excretion) *
    Val(cmilk_yield, Excretion::CMilk);
?else
  ++formula
    if ( (not defined In(inp_n_excretion)) or lc In(inp_n_excretion) eq 'standard' ) {
      Tech(standard_N_excretion) *
      Val(cmilk_yield, Excretion::CMilk) *
      Val(c_feed_ration, Excretion::CFeed);
    } else {
      if ( (In(inp_n_excretion) < 0.7 * Tech(standard_N_excretion)) or 
           (In(inp_n_excretion) > 1.3 * Tech(standard_N_excretion)) ) {
        writeLog({
            en => "The N excretion entered for dairy cows differs from the standard by more than 30%!",
            de => "Die eingegebene N-Ausscheidung für Milchkühe weicht um mehr als 30% vom Standard ab!",
            fr => "Les excrétions azotées saisies pour les vâches laitières s'écartent de plus de 30 % du standard!",
        });
      }
      In(inp_n_excretion);
    };
?endif

+n_excretion
  print = DairyCow
  ++units
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual total N excreted by the specified number of animals.
  ++formula
    Out(n_excretion_animal) *
    Out(animals);

?if !Kantonal_LU
  +tan_content
    print = DairyCow
    ++units
      en = -
      de = -
      fr = -
    ++description
      TAN content (as fraction) of the dairy cow excretion.
    ++formula
      if ( (not defined In(tan_fraction)) or lc In(tan_fraction) eq 'standard' ) {
        if ( (not defined In(inp_n_excretion)) or lc In(inp_n_excretion) eq 'standard' ) {
          return ( Tech(share_Nsol) - Tech(feed_influence_on_Nsol) ) / Val(c_feed_ration, Excretion::CFeed) +
            Tech(feed_influence_on_Nsol);
        } else {
          return Tech(share_Nsol);
        }
      } else {
        my $tan = In(tan_fraction);
        $tan = In(tan_fraction) / 100;
        if ( abs($tan - Tech(share_Nsol)) > 0.2) {
          writeLog({
            en => "The TAN fraction of N excretion entered for diary cows differs from the standard by more than 20%!",
            de => "Der eingegebene TAN Anteil der N Ausscheidung für Milchkühe weicht um mehr als 20% vom Standard ab!",
            fr => "La proportion du TAN des excrétions azotées saisies pour les vâches laitières s'écartent de plus de 20 % du standard!"
          });
        }
        return $tan;
      }  
?endif

+tan_excretion
  print = DairyCow
  ++units
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual soluble N excreted by the specified number of animals.
?if Kantonal_LU
  ++formula
    Out(n_excretion) * Tech(share_Nsol);
?else
  ++formula
    Out(n_excretion) * Out(tan_content);
?endif

+dimensioning_barn
  print = DairyCow
  ++units
    en = -
  ++description
    barn size (number of animal places)
  ++formula
    In(dimensioning_barn);

+area_increase
  print = DairyCow
  ++units 
    en = -
  ++description
    Factor on what barn size does increase the regularized minimal, limited to 0.5
  ++formula 
    if ( (Out(animals) < Out(dimensioning_barn) ) and (Out(animals) != 0) ) { 
      if ( Out(dimensioning_barn) >= (Out(animals) * 1.5) ) {   
        0.5; 
      }
      else {
        Out(dimensioning_barn) / Out(animals) - 1;
      }
    }
    else {
      0.0;
    }

### former DairyCow.nhd
+dimensioning_check
  print = DairyCow
  ++units
    en = -
  ++description
   Check if number of animals <= number of animal places.
  ++formula
    if ( Out(dimensioning_barn) < Out(animals) ) {
      writeLog({
        en => "The number of available animal places must be equal or higher than the number of animals.",
        de => "Anzahl Tierplätze muss grösser oder gleich der Anzahl der Tiere sein.",
        fr => "Le nombre de places dans l'étable doit être au moins égal au nombre d'animaux."
      });
      return "barn dimensioning too small";
    } 
    else {
      return "barn dimensioning ok";
    }

