*** general ***

author   = Agrammon Group
date     = 2008-02-29
taxonomy = Storage
gui      = Storage,Hofdüngerlager,Stockage,Storage

+short

 Computes the annual NH3 emission from manure
 storage based on a categorised slurry type and the cover type.

+description 

  This process calculates the NH3 emission from slurry storage,
  considering both slurry from slurry based systems and liquid from
  liquid/solid systems. The surface to volume ration (measure for the
  emitting surface), the cover type and artificial slurry aeration are
  accounted for via correction factors. Calculations are performed
  independently for slurry and liquid from liquid/solid systems with
  the same procedure.

  \subsubsection{References:}
  
  De Bode MJC, 1990. Vergleich der Ammoniakemissionen aus verschiedenen
  Flüssigmistlagersystemen. In: Ammoniak in der Umwelt. Hrsg.: KTBL
  und VDI, Münster, 34:1-13.


*** technical ***

+mineralizationrate_liquid
  value = 0.1         
  ++units  
    en = -
  ++description
    A netto mineralization of 10% from Norg to NSol/TAN is assuemd, according to
  the GAS$_{\rm{EM}}$ Model
  


*** external ***

+Storage::SolidManure
+Storage::SolidManure::Solid
+Storage::SolidManure::Poultry
+Livestock
+Storage::Slurry
  aggregate=SUM

*** output ***

+nh3_nstorage
  format= %.0f
  print = LivestockSummary
  ++labels 
    sort = 802
    en = Storage NH3-Emissions
    de = Hofdüngerlagerung NH3-Emission
    fr = Emission de NH3 Stockage des engrais de ferme 
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from storage.
  ++formula
    Sum(nh3_nliquid, Storage::Slurry) + 
    Val(nh3_nsolid, Storage::SolidManure);


+nh3_nstorage_solid
  format= %.0f
  print = LivestockSummaryPlus
  ++labels 
    sort = 804
    en = _Storage solid NH3-Emissions
    de = _Hofdüngerlagerung fest NH3-Emission
    fr = _Emission de NH3 Stockage des engrais de ferme solides 
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from storage.
  ++formula
    Val(nh3_nsolid, Storage::SolidManure);

+nh3_nstorage_liquid
  format= %.0f
  print = LivestockSummaryPlus
  ++labels 
    sort = 803
    en = _Storage liquid NH3-Emissions
    de = _Hofdüngerlagerung flüssig NH3-Emission
    fr = _Emission de NH3 Stockage des engrais de ferme liquides 
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from storage.
  ++formula
    Sum(nh3_nliquid, Storage::Slurry);

+has_liquid_storage
  format= %.0f
  print = checkComment
  ++labels 
    en = True (1) if a Liquid Storage (Volume) is present.
    de = Wert ist wahr (1) wenn flüssig lager volumen vorhanden ist.
    fr = Indiquer au moyen d'un volume des Stockage des engrais de ferme liquides est present.
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    True (1) if a Liquid Storage (Volume) is present.
  ++formula
    if( Sum(volume, Storage::Slurry) > 0){
        return 1;
    }else{
        if( Val(n_into_storage_liquid_check, Livestock) > 0 ){
          writeLog({en => "No storage for slurry defined although slurry is produced!\n",
                    de => "Es ist kein Güllelager eingegeben, obwohl Gülle anfällt!\n",
                    fr => "Aucun stock de lisier n’est mentionné, alors qu’il y a "
                          . "production de lisier!\n"
                   });  
        }
        return 0;
   }
    
+mineralization_liquid 
  print = 7
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual TAN mineralized from not TAN fraction in liquid storage.
  ++formula

    if(Val(tan_into_storage_liquid, Livestock) <  Sum(nh3_nliquid, Storage::Slurry))
      {0} 
    else 
      {
     Tech(mineralizationrate_liquid) * 
     ( Val(n_into_storage_liquid, Livestock) -  Val(tan_into_storage_liquid,
      Livestock) )
     }

+n_into_application_liquid
  print = 3x
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for application.
  ++formula
    if(Val(n_into_storage_liquid, Livestock) < Sum(nh3_nliquid, Storage::Slurry)  )
      {0} 
    else 
      { Val(n_into_storage_liquid, Livestock) -
        Val(nxox_nliquid, Livestock) -
        Sum(nh3_nliquid, Storage::Slurry)
        };

 +tan_into_application_liquid
  print = 4x
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux as TAN out of storage for application.
  ++formula
    if( Val(tan_into_storage_liquid, Livestock) < ( Sum(nh3_nliquid, Storage::Slurry) + Val(nxox_nliquid, Livestock) ) )
      {0} 
    else 
      { Val(tan_into_storage_liquid, Livestock) -
        Sum(nh3_nliquid, Storage::Slurry) -
        Val(nxox_nliquid, Livestock) +
        Out(mineralization_liquid) };


+n_into_application_manure
  print = 3a
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for manure application.
  ++formula
    Val(n_into_application_dairycows_cattle, Storage::SolidManure::Solid) +
    Val(n_into_application_pigs, Storage::SolidManure::Solid) +
    Val(n_into_application_horses_otherequides_smallruminants, Storage::SolidManure::Solid) +
    Val(n_into_application_turkeys_broilers, Storage::SolidManure::Poultry) +
    Val(n_into_application_layers_growers_other_poultry, Storage::SolidManure::Poultry) 
 

+tan_into_application_manure
  print = 3a
  ++units  
    en = kg TAN/year
    de = kg TAN/Jahr
    fr = kg TAN/an
  ++description
    Annual TAN flux out of storage for manure application.
  ++formula
    Val(tan_into_application_dairycows_cattle, Storage::SolidManure::Solid) +
    Val(tan_into_application_pigs, Storage::SolidManure::Solid) +
    Val(tan_into_application_horses_otherequides_smallruminants, Storage::SolidManure::Solid) +
    Val(tan_into_application_turkeys_broilers, Storage::SolidManure::Poultry) +
    Val(tan_into_application_layers_growers_other_poultry, Storage::SolidManure::Poultry);

+n_into_application
  format= %.0f
  print = FluxSummaryLivestock
  ++labels 
    sort = 108
    en = Total N into application
    de = Total N in die HofdÃ¼ngerausbringung
    fr = N total dans l'Ã©pandage des engrais de ferme
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for application.
  ++formula
    Out(n_into_application_liquid) + 
    Out(n_into_application_manure) ;

+tan_into_application
  format= %.0f
  print = TANFlux
  ++labels 
    sort = 208
    en = Total TAN into application
    de = Total NlÃ¶s in die HofdÃ¼ngerausbringung
    fr = TAN total dans l'Ã©pandage des engrais de ferme
  ++units  
    en = kg TAN/year
    de = kg TAN/Jahr
    fr = kg TAN/an
  ++description
    Annual TAN flux out of storage for application.
  ++formula
    Out(tan_into_application_liquid) + 
    Out(tan_into_application_manure);





