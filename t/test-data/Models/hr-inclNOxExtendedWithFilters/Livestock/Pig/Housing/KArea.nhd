*** general ***

author   = Agrammon Group
date     = 2008-04-22
taxonomy = Livestock:Pig::Housing::KArea

+short

  Calculates correction factor according to the area per animal in
  the house.

+description

  It is assumed that additional areas in stable, as example if the stabel is
  built for more animal, are fully used. 
  So the emissions increase 5\% per 10\% additional area in a stable up to
  the double of the regularized minimal area.

  \begin{todo}{All}{}
	Provide Articles which confirm/justify the assumption. As decided on
	workshop from 2.11.2007.
  \end{todo}


*** input ***

+dimensioning_barn
  type = integer
  adjustBranching = true
  ++labels 
    en = Number of animal places in building of category
    de = Stallkapazitaet (Anzahl Tierplaetze der Kategorie)
    fr = Places dans la stabulation (Nombre de places d'animaux par cat√©gorie d'animaux)
  ++units  
    en = -
  ++description
    Number of animals which area allowed in a barn.

*** technical ***

+k_area
  value = 0.5
  ++units  
    en = -
  ++description
    Increasing factor for larger loose housing barns, 10 0/0 bigger leads to 5  0/0 more emission.

*** external ***
+..::Excretion 



*** output ***

+area_increase
  print = 15
  ++units  
    en = -
  ++description
    Factor on what barn size does increase the regularized minimal, limited to 0.5

  ++formula 
    if(    ( Val(pigs,..::Excretion) < In(dimensioning_barn) )
       and ( Val(pigs, ..::Excretion) != 0 ) ) { 
    	 if( In(dimensioning_barn) >= (Val(pigs,..::Excretion) * 1.5) ){   
 	     return 0.5; 
 	 }
 	 else { 
 	     return( ( In(dimensioning_barn) / Val(pigs, ..::Excretion) ) -1 );
        }  			
    }
    else {
 	return 0.0;
    };

+k_area
  print = 15
  ++units  
    en = -
  ++description
    correction factor for area per animal

  ++formula
    1 + ( Out(area_increase) * Tech(k_area) );
