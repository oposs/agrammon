*** general ***

author   = Agrammon Group
date     = 2009-02-22
taxonomy = Livestock::Pig::Housing

+short

 Computes the annual NH3 emission from pig housing systems.

+description 

 This process calculates the NH3 emission in pig housing
 depending on the N excretion and the housing systems. The NH3 emission is assumed to be proportional to the total N excretion of the animals because the contaminated surfaces will primarily drive emissions, which will remain active even when animals are on the pasture or exercise yard.

  A proportional correction is applied according to the area per
  animal.

  A correction is applied if the part of the day grazing is above a
  defined grazing hours per days.

  \subsubsection{Definition of System boundaries for the housing Process:}

  For housing systems with integrated exercise yards, it is difficult to
  distinguish between emissions from housing, exercise yard and storage. 
  For these housingtypes the emission may be only validated for the sum of
  housing, exercise yard and storage emission.
  
*** input ***

*** external ***

+Excretion
+Grazing
+Housing::Type
+Housing::AirScrubber
+Housing::MitigationOptions
+Housing::CFreeFactor

*** output ***

+n_into_housing
  print = 7
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N flux into the house.
  ++formula
    Val(n_excretion, Excretion)
    - Val(n_into_grazing, Grazing);

+tan_into_housing
  print = 7
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N flux as TAN into the house.
  ++formula
    Val(tan_excretion, Excretion)
       - Val(tan_into_grazing, Grazing);

+nh3_nhousing
  print = 5b
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual NH3 emission from pig housing systems.
  ++formula
   Val(tan_excretion, Excretion) 
    * Val(er_housing, Housing::Type) 
    * (
         (1 - Val(c_air_scrubber, Housing::AirScrubber)*Val(share_outdoor, Housing::Type) )
         * (1 - Val(c_UNECE_housing_task, Housing::MitigationOptions)*Val(share_outdoor, Housing::Type) )
         * (1 - Val(c_housing_slurry_channel, Housing::MitigationOptions)*Val(share_outdoor, Housing::Type ) )
         * (1 - Val(c_housing_climate, Housing::MitigationOptions)*Val(share_outdoor, Housing::Type ) )
         * (1 - Val(c_housing_air, Housing::MitigationOptions)*Val(share_outdoor, Housing::Type))  -
          Val(c_exercise_place, Housing::Type)
       )
    * Val(c_free_factor_housing, Housing::CFreeFactor) ; 
	   

+n_housing_filter
  print = 5b
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N of NH3 emission remaining in air scrubber from pig housing systems.
  ++formula
   Val(tan_excretion, Excretion) 
    * Val(er_housing, Housing::Type) 
    * Val(c_air_scrubber, Housing::AirScrubber) 
    * (
         (1 - Val(c_UNECE_housing_task, Housing::MitigationOptions)*Val(share_outdoor, Housing::Type) )
         * (1 - Val(c_housing_slurry_channel, Housing::MitigationOptions)*Val(share_outdoor, Housing::Type ) )
         * (1 - Val(c_housing_climate, Housing::MitigationOptions)*Val(share_outdoor, Housing::Type ) )
         * (1 - Val(c_housing_air, Housing::MitigationOptions)*Val(share_outdoor, Housing::Type))  -
          Val(c_exercise_place, Housing::Type)
       ); 


+n_outhousing
  print = 7
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N flux out of the housing including N remained in biotrickling filter.
  ++formula
     if(Val(housing_type, Housing::Type) eq 'Outdoor'){
        0
     }else {
     	   if(Val(air_scrubber, Housing::AirScrubber) eq 'acid'){
	   Out(n_into_housing) - Out(nh3_nhousing) - Out(n_housing_filter)
     	   }else{
	   Out(n_into_housing) - Out(nh3_nhousing)
	   };
     };

+tan_outhousing
  print = 9
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N flux as TAN out of the housing indcluding N remained in biotrickling filter.
    (without remains from acid filter)
  ++formula
    if(Val(housing_type, Housing::Type) eq 'Outdoor'){
        0
    }else {
     	   if(Val(air_scrubber, Housing::AirScrubber) eq 'acid'){
	   Out(tan_into_housing) - Out(nh3_nhousing) - Out(n_housing_filter)
     	   }else{
    	   Out(tan_into_housing) - Out(nh3_nhousing)
	   };
    };

+share_tan_out
  print = 8
  ++units  
    en = -
  ++description 
    Share of TAN for interface to storage, the same share is assumed for 
    solid and liquid parts of output. Mineralization and immobilization
    are considerd in the storage module.
  ++formula
    if(  Out(n_outhousing) != 0 ){
 	Out(tan_outhousing) / Out(n_outhousing)
    }else{0};

+n_outhousing_liquid
  print = 7
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N flux out of housing, slurry or liquid fraction from pigs. 
  ++formula
    if(Val(housing_type, Housing::Type) eq 'Outdoor'){
       0
    }else {
       Out(n_outhousing) * Val(share_liquid, Housing::Type)
    };

+tan_outhousing_liquid
  print = 7
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N flux as TAN out of housing, slurry or liquid fraction from pigs. 
  ++formula
    if(Val(housing_type, Housing::Type) eq 'Outdoor'){
       0
    }else {
       Out(n_outhousing) * Val(share_liquid, Housing::Type) * Out(share_tan_out)
    };

+n_outhousing_solid
  print = 7
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N flux out of housing, manure fraction of N flux from pigs.
  ++formula
    if(Val(housing_type, Housing::Type) eq 'Outdoor'){
       0
    }else {
       Out(n_outhousing) * ( 1 - Val(share_liquid, Housing::Type) )
    };

+tan_outhousing_solid
  print = 7
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an	
  ++description
    Annual N flux as TAN out of housing, manure fraction of N flux from pigs.  
  ++formula
   if(Val(housing_type, Housing::Type) eq 'Outdoor'){
        0
    }else {
        Out(n_outhousing) * ( 1 - Val(share_liquid, Housing::Type) ) * Out(share_tan_out)
   };
 
