*** general ***

author   = Agrammon Group
date     = 2008-02-19
taxonomy = Livestock::OtherCattle::Housing::KArea

+short

  Calculates correction factor according to the area per animal in
  the house.

+description

  It is assumed that additional areas in stable, as example if the stabel is
  built for more animal, are fully used. 
  So the emissions increase 5\% per 10\% additional area in a stable up to
  the double of the regularized minimal area.

  \begin{todo}{All}{}
	Provide Articles which confirm/justify the assumption. As decided on
	workshop from 2.11.2007.
  \end{todo}


*** input ***

+dimensioning_barn
  type = integer
  adjustBranching = true
  ++labels 
    en = Dimensioning of barn
    de = Stallkapazitaet (Anzahl Tierplaetze der Kategorie) 
    fr = Places dans la stabulation (nombre de places par cat√©gorie d'animaux)
  ++units 
    en = -
  ++description
    Number of Animals which area allowed in a barn.

*** technical ***

+k_area_tide_housing_slurry
  value = 0.0
  ++units 
    en = -
  ++description
    Increasing factor for larger tide housing barns

+k_area_tide_housing_liquid_solid
  value = 0.0
  ++units 
    en = -
  ++description
    Increasing factor for larger tide housing barns

+k_area_loose_housing_slurry
  value = 0.5
  ++units 
    en = -
  ++description
    Increasing factor for larger loose housing barns, 10 0/0 bigger leads to 5 0/0 more emission.

+k_area_loose_housing_liquid_solid
  value = 0.5
  ++units 
    en = -
  ++description
    Increasing factor for larger loose housing barns, 10 0/0 bigger leads to 5 0/0 more emission.

+k_area_loose_housing_deep_litter
  value = 0.5
  ++units 
    en = -
  ++description
    Increasing factor for larger loose housing barns, 10 0/0 bigger leads to 5 0/0 more emission.

*** external ***
+ERCattleHousing
+..::Excretion


*** output ***

+area_increase
  print = 15
  ++units 
    en = -
  ++description
    Factor on what barn size does increase the regularized minimal, limited to 0.5

  ++formula 
    if( ( Val(animals, ..::Excretion) < In(dimensioning_barn)  ) and ( Val(animals, ..::Excretion) != 0 ) ){ 
    	 if( In(dimensioning_barn) >= (Val(animals, ..::Excretion) * 1.5) ){   
	     return 0.5; 
	 }
	 else { 
	     return( ( In(dimensioning_barn) / Val(animals, ..::Excretion) ) -1 );
        }  			
    }
    else {
	return 0.0;
    };

+k_area
  print = 15
  ++units 
    en = -
  ++description
    correction factor for area per animal

  ++formula
    my $key = "k_area_" . Val(housing_type, ERCattleHousing);
    return 1 + ( Out(area_increase) * Tech($key) );
