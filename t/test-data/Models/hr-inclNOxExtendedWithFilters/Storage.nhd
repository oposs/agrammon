*** general ***

author   = Agrammon Group
date     = 2008-02-29
taxonomy = Storage
gui      = Storage,Hofdüngerlager,Stockage,Storage

+short

 Computes the annual NH3 emission from manure
 storage based on a categorised slurry type and the cover type.

+description 

  This process calculates the NH3 emission from slurry storage,
  considering both slurry from slurry based systems and liquid from
  liquid/solid systems. The surface to volume ration (measure for the
  emitting surface), the cover type and artificial slurry aeration are
  accounted for via correction factors. Calculations are performed
  independently for slurry and liquid from liquid/solid systems with
  the same procedure.

  \subsubsection{References:}
  
  De Bode MJC, 1990. Vergleich der Ammoniakemissionen aus verschiedenen
  Flüssigmistlagersystemen. In: Ammoniak in der Umwelt. Hrsg.: KTBL
  und VDI, Münster, 34:1-13.


*** technical ***

+mineralizationrate_liquid
  value = 0.1         
  ++units  
    en = -
  ++description
    A netto mineralization of 10% from Norg to NSol/TAN is assuemd, according to
  the GAS$_{\rm{EM}}$ Model
  


*** external ***

+Storage::SolidManure
+Storage::SolidManure::Solid
+Storage::SolidManure::Poultry
+Livestock
+Storage::Slurry
  aggregate=SUM

*** output ***

 
+has_liquid_storage
  format= %.0f
  print = checkComment
  ++labels 
    en = True (1) if a Liquid Storage (Volume) is present.
    de = Wert ist wahr (1) wenn flüssig lager volumen vorhanden ist.
    fr = Indiquer au moyen d'un volume des Stockage des engrais de ferme liquides est present.
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    True (1) if a Liquid Storage (Volume) is present.
  ++formula
    my $volume = Sum(volume, Storage::Slurry) // 0;
    if ( $volume > 0){
      return 1;
    }
    else {
      if( Val(n_into_storage_liquid_check, Livestock) > 0 ) {
        writeLog(
            {
              en => "No storage for slurry defined although slurry is produced!\n",
              de => "Es ist kein Güllelager eingegeben, obwohl Gülle anfällt!\n",
              fr => "Aucun stock de lisier n'est mentionné, alors qu'il y a production de lisier!\n"
            }
        );  
      }
      return 0;
   }



####################################################
##### mineralization liquid
    
+mineralization_liquid_cattle 
  print = 7
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual TAN mineralized from not TAN fraction in liquid storage.
  ++formula
    scale( 
      Val(n_out_livestock_liquid_cattle, Livestock) P-  
      Val(tan_into_storage_liquid_cattle, Livestock), 
      Tech(mineralizationrate_liquid)
    );
    
+mineralization_liquid_pigs 
  print = 7
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual TAN mineralized from not TAN fraction in liquid storage.
  ++formula
    scale( 
      Val(n_out_livestock_liquid_pigs, Livestock) P- 
      Val(tan_into_storage_liquid_pigs, Livestock), 
      Tech(mineralizationrate_liquid)
    );
    
+mineralization_liquid 
  print = 7
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual TAN mineralized from not TAN fraction in liquid storage.
  ++formula
    Out(mineralization_liquid_cattle) P+ 
    Out(mineralization_liquid_pigs);

####################################################


####################################################
##### nh3 loss
 
+nh3_nstorage_solid
  format= %.0f
  print = LivestockSummaryPlus
  ++labels 
    sort = 804
    en = _Storage solid NH3-Emissions
    de = _Hofdüngerlagerung fest NH3-Emission
    fr = _Emission de NH3 Stockage des engrais de ferme solides 
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from storage.
  ++formula
    Val(nh3_nsolid, Storage::SolidManure);

+nh3_nstorage_liquid_pigs
  print = newStorage
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from liquid storage of pig slurry.
  ++formula
    my $max_pigs =  Val(tan_into_storage_liquid_pigs, Livestock) P+ 
                      Out(mineralization_liquid_pigs) P-
                      Val(nxox_nliquid_pigs, Livestock);
    if ( scalar(Sum(nh3_nliquid_pigs, Storage::Slurry)) <  scalar($max_pigs) ) {
      scale( Val(n_out_livestock_liquid_pigs_ratio, Livestock), Sum(nh3_nliquid_pigs, Storage::Slurry) );
    } else {
      # writeLog(
      #     {
      #       en => "No storage for slurry defined although slurry is produced!\n",
      #       de => "Es ist kein Güllelager eingegeben, obwohl Gülle anfällt!\n",
      #       fr => "Aucun stock de lisier n'est mentionné, alors qu'il y a production de lisier!\n"
      #     }
      # );  
      $max_pigs; 
    }

+nh3_nstorage_liquid_cattle
  print = newStorage
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from liquid storage of cattle slurry.
  ++formula
    my $max_cattle =  Val(tan_into_storage_liquid_cattle, Livestock) P+ 
                      Out(mineralization_liquid_cattle) P-
                      Val(nxox_nliquid_cattle, Livestock);
    if ( scalar(Sum(nh3_nliquid_cattle, Storage::Slurry)) <  scalar($max_cattle) ) {
      scale( Val(n_out_livestock_liquid_cattle_ratio, Livestock), Sum(nh3_nliquid_cattle, Storage::Slurry) );
    } else {
      # writeLog(
      #     {
      #       en => "No storage for slurry defined although slurry is produced!\n",
      #       de => "Es ist kein Güllelager eingegeben, obwohl Gülle anfällt!\n",
      #       fr => "Aucun stock de lisier n'est mentionné, alors qu'il y a production de lisier!\n"
      #     }
      # );  
      $max_cattle; 
    }

+nh3_nstorage_liquid
  format= %.0f
  print = LivestockSummaryPlus
  ++labels 
    sort = 803
    en = _Storage liquid NH3-Emissions
    de = _Hofdüngerlagerung flüssig NH3-Emission
    fr = _Emission de NH3 Stockage des engrais de ferme liquides 
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from storage.
  ++formula
    Out(nh3_nstorage_liquid_pigs) P+ 
    Out(nh3_nstorage_liquid_cattle);

+nh3_nstorage
  format= %.0f
  print = LivestockSummary
  ++labels 
    sort = 802
    en = Storage NH3-Emissions
    de = Hofdüngerlagerung NH3-Emission
    fr = Emission de NH3 Stockage des engrais de ferme 
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from storage.
  ++formula
    Out(nh3_nstorage_liquid) P+ 
    Out(nh3_nstorage_solid);

####################################################



####################################################
##### n into application liquid

+n_into_application_liquid_pigs
  print = newStorage
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for application.
  ++formula
    Val(n_out_livestock_liquid_pigs, Livestock) P-
    Out(nh3_nstorage_liquid_pigs) P-
    Val(nxox_nliquid_pigs, Livestock);


+n_into_application_liquid_cattle
  print = newStorage
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for application.
  ++formula
    Val(n_out_livestock_liquid_cattle, Livestock) P-
    Out(nh3_nstorage_liquid_cattle) P-
    Val(nxox_nliquid_cattle, Livestock);


+n_into_application_liquid
  print = newStorage
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for application.
  ++formula
    Out(n_into_application_liquid_pigs) P+
    Out(n_into_application_liquid_cattle);

####################################################


####################################################
##### tan into application liquid

+tan_into_application_liquid_pigs
  print = newStorage
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for application.
  ++formula
    Val(tan_into_storage_liquid_pigs, Livestock) P-
    Out(nh3_nstorage_liquid_pigs) P-
    Val(nxox_nliquid_pigs, Livestock) P+
    Out(mineralization_liquid_pigs);

+tan_into_application_liquid_cattle
  print = newStorage
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for application.
  ++formula
    Val(tan_into_storage_liquid_cattle, Livestock) P-
    Out(nh3_nstorage_liquid_cattle) P-
    Val(nxox_nliquid_cattle, Livestock) P+
    Out(mineralization_liquid_cattle);

+tan_into_application_liquid
  print = newStorage
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux as TAN out of storage for application.
  ++formula
    Out(tan_into_application_liquid_pigs) P+
    Out(tan_into_application_liquid_cattle);

+tan_into_application_liquid_pigsratio_scalar
  print = newApplication
  ++units
    en = -
    de = -
    fr = -
  ++description
    Scalar. Pigs share of annual liquid N flux as TAN from storage into application (scaled by total flux).
  ++formula
    scalar( Out(tan_into_application_liquid_pigs) ) / scalar( Out(tan_into_application_liquid) );

####################################################


####################################################
##### n/tan into application solid

+n_into_application_solid
  print = 3a
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for manure application.
  ++formula
    Val(n_into_application_solid_cattle, Storage::SolidManure::Solid)   P+
    Val(n_into_application_solid_pigs, Storage::SolidManure::Solid)   P+
    Val(n_into_application_solid_others, Storage::SolidManure::Solid)   P+
    Val(n_into_application_solid_poultry, Storage::SolidManure::Poultry) 
 

+tan_into_application_solid
  print = 3a
  ++units  
    en = kg TAN/year
    de = kg TAN/Jahr
    fr = kg TAN/an
  ++description
    Annual TAN flux out of storage for manure application.
  ++formula
    Val(tan_into_application_solid_cattle, Storage::SolidManure::Solid) P+
    Val(tan_into_application_solid_pigs, Storage::SolidManure::Solid) P+
    Val(tan_into_application_solid_others, Storage::SolidManure::Solid) P+
    Val(tan_into_application_solid_poultry, Storage::SolidManure::Poultry);

####################################################


####################################################
##### n/tan into application (remove later?)

+n_into_application
  format= %.0f
  print = FluxSummaryLivestock
  ++labels 
    sort = 108
    en = Total N into application
    de = Total N in die Hofdüngerausbringung
    fr = N total dans l'Epandage des engrais de ferme
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual N flux out of storage for application.
  ++formula
    Out(n_into_application_liquid) P+ 
    Out(n_into_application_solid) ;

+tan_into_application
  format= %.0f
  print = TANFlux
  ++labels 
    sort = 208
    en = Total TAN into application
    de = Total Nlös in die Hofdüngerausbringung
    fr = TAN total dans l'Epandage des engrais de ferme
  ++units  
    en = kg TAN/year
    de = kg TAN/Jahr
    fr = kg TAN/an
  ++description
    Annual TAN flux out of storage for application.
  ++formula
    Out(tan_into_application_liquid) P+ 
    Out(tan_into_application_solid);

####################################################




#+n_out_storage_solid
  # print = 7
  # ++units  
  #   en = kg N/year
  #   de = kg N/Jahr
  #   fr = kg N/an
  # ++description
  #   Annual N flux from solid storage to application.
  # ++formula
  #   Val(n_out_livestock_solid, ::Livestock) P-
  #   Out(nh3_nsolid) P-
  #   Val(nxox_nsolid, ::Livestock)

