use v6;
use Agrammon::Config;
use Agrammon::DB::User;
use DB::Pg;
use Test;

use lib 't/lib';
use AgrammonTest;

plan 4;

if %*ENV<AGRAMMON_UNIT_TEST> {
    skip-rest 'Not a unit test';
    exit;
}

my $*AGRAMMON-DB-CONNECTION;

subtest 'Connect to database' => {
    my $conninfo;
    my $cfg-file;
    if %*ENV<DRONE_REPO> {
        $cfg-file = %*ENV<AGRAMMON_CFG> // "t/test-data/agrammon.drone.cfg.yaml";
    }
    else {
        $cfg-file = %*ENV<AGRAMMON_CFG> // "t/test-data/agrammon.cfg.yaml";
    }
    my $cfg = Agrammon::Config.new;
    lives-ok { $cfg.load($cfg-file) }, "Load config from file $cfg-file";
    $conninfo = $cfg.db-conninfo;
    ok $*AGRAMMON-DB-CONNECTION = DB::Pg.new(:$conninfo), 'Create DB::Pg object';
}

subtest 'Create user' => {
    ok my $user = Agrammon::DB::User.new(
        :username<agtest>,
        :firstname<XF>,
        :lastname<XL>,
        :organisation<XO>,
        :password<XP123456>,
    ), 'Create new user';
    is $user.username,     'agtest', 'User has correct username';
    is $user.firstname,    'XF',     'User has correct firstname';
    is $user.lastname,     'XL',     'User has correct lastname';
    is $user.organisation, 'XO',     'User has correct organisation';
    is $user.password,     'XP123456', 'User has correct password';
}

transactionally {
    my $username = 'agtest';
    my $password = 'XP123456';
    my $uid;
    my $new-user;

    # ok prepare-test-db, 'Test database prepared';

    subtest 'create-account()' => {
        ok my $user = Agrammon::DB::User.new(
            :$username,
            :firstname<XF>,
            :lastname<XL>,
            :organisation<XO>,
            :$password,
        ), 'Create new user';
        ok my $key = $user.create-account('admin'), "Create account, key=$key";
        ok $uid = $user.activate-account($key).id, "Activate account, uid=$uid";

        is $user.username, $username, "Username is $username";
        is $user.role.name, 'admin', "User role is admin";
        throws-like {my $key = $user.create-account('admin')},
                    X::Agrammon::DB::User::Exists, "Cannot create existing account";
    }

    subtest 'load()' => {
        ok $new-user = Agrammon::DB::User.new, "Create Agrammon::DB::User object without username";
        throws-like {$new-user.load}, X::Agrammon::DB::User::NoUsername, "No username";
        ok $new-user = Agrammon::DB::User.new(:$username), "Create another Agrammon::DB::User object with username";
        ok $new-user.load,                "Load new user";
        is $new-user.username, $username, "Username is $username";
    }

}

done-testing;
